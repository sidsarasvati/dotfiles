#+TITLE: Sid's Emacs Configuration
#+AUTHOR: Sid Sarasvati
#+DESCRIPTION: A literate programming style Emacs configuration
#+STARTUP: overview
#+OPTIONS: toc:4 h:4

* Introduction

This is my personal Emacs configuration, built and maintained since 2012. I've converted it to a literate programming style to make it more maintainable and understandable for the future.

Literate programming allows me to:
- Document my configuration decisions
- Keep related code blocks together
- Provide context for why certain settings exist
- Make it easier to reorganize and refactor

** How it works

This file (=config.org=) contains all the configuration in organized
sections. Emacs loads this file and evaluates the code blocks within it. The
actual =.emacs= file just contains the code to load this configuration.

** Setup and Usage

To use this configuration:
1. Clone the dotfiles repository
2. Use GNU Stow to create the necessary symlinks
3. Launch Emacs

* Basic Settings and Bootstrap

** Debugging

We can enable error backtraces during startup for debugging purposes.

#+begin_src emacs-lisp
  ;;(setq debug-on-error t) ; get backtrace or errors
#+end_src

** Package Management

First, we need to set up the package system and initialize the package repositories.

#+begin_src emacs-lisp
  ;;------------------------------------------------------------------------------
  ;; Package Management: Package Listing and Auto Installation
  ;;-----------------------------------------------------------------------------
  ; list the packages you want
  (setq package-list '(yaml-mode cmake-mode))

  ; list the repositories containing them (updated Dec 2025)
  (setq package-archives '(("gnu" . "https://elpa.gnu.org/packages/")
                           ("nongnu" . "https://elpa.nongnu.org/nongnu/")
                           ("melpa" . "https://melpa.org/packages/")))

  ; activate all the packages (in particular autoloads)
  (package-initialize)

  ; fetch the list of packages available 
  (unless package-archive-contents
    (package-refresh-contents))

  ; install the missing packages - DOESN'T WORK!!
  (dolist (package package-list)
    (unless (package-installed-p package)
      (package-install package)))
#+end_src


** Load Path Configuration

Set the load path for custom libraries and packages.

#+begin_src emacs-lisp
  ;;------------------------------------------------------------------------------
  ;; Set Load Path (Should be first)
  ;;------------------------------------------------------------------------------
  (add-to-list 'load-path "~/.emacs.d/elisp")

  (let ((default-directory  "~/.emacs.d/elpa/"))
    (normal-top-level-add-subdirs-to-load-path))
#+end_src

** Shell Path Integration (macOS)

On macOS, GUI Emacs doesn't inherit shell PATH. This fixes it.

#+begin_src emacs-lisp :results silent
  ;; Copy PATH from shell so npm, node, etc. are found
  (when (memq window-system '(mac ns x))
    (use-package exec-path-from-shell
      :ensure t
      :config
      (exec-path-from-shell-initialize)))
#+end_src

** Basic Editor Configuration

Set some basic editor defaults for tabs, indentation, and key bindings.

#+begin_src emacs-lisp
  (setq default-tab-width 2)              ; Tab width = 2
  (setq-default indent-tabs-mode nil)     ; Tab inserts spaces only
  (setq mac-command-modifier 'meta)       ; Sets the command (Apple) key as Meta
#+end_src

** Backup and Auto-Save Files

Configure Emacs to centralize backup and auto-save files instead of scattering them throughout the file system.

#+begin_src emacs-lisp
  ;; Get user-emacs-directory for portability
  (defvar backup-dir (expand-file-name "backups" user-emacs-directory))
  (defvar auto-save-dir (expand-file-name "auto-saves" user-emacs-directory))
  
  ;; Store backup files in a central location
  (setq backup-directory-alist
        `(("." . ,backup-dir)))
  
  ;; Make backup files even when file is under version control
  (setq vc-make-backup-files t)
  
  ;; Store auto-save files in a central location
  (setq auto-save-file-name-transforms
        `((".*" ,auto-save-dir t)))
  
  ;; Create the backup and auto-save directories if they don't exist
  (make-directory backup-dir t)
  (make-directory auto-save-dir t)
  
  ;; Disable lock files (.#file.txt)
  (setq create-lockfiles nil)
#+end_src

* UI and Appearance

** Global UI Configuration

Configure the global UI elements and behavior.

#+begin_src emacs-lisp
  ;;------------------------------------------------
  ;; Global Emacs Config {
  ;;------------------------------------------------

  ;; load custom scripts 
  (load "scroll.el") ;; scrolling 
  (load "my-convenience.el") ;; key binding
  (load "gud.el") ;; lldb mode

  ;; Preferences                          *** ----------------------------
  (setq make-backup-files nil)            ;;; disable backup files
  (setq column-number-mode t)             ;;; Put column number in display
  (setq-default fill-column 80)           ;;; M-q to fill col; width set to 80
  (setq inhibit-startup-screen t)         ;;; Disable welcome screen
  (setq initial-scratch-message nil)      ;;; Empty scratch buffer
                                          ;;; -----------------------------
  ;; clang format
  ;; (require 'clang-format)

  ;; Change "yes or no" to "y or n"
  (fset 'yes-or-no-p 'y-or-n-p)
#+end_src

** Mode Line Customization (LEGACY - superseded by doom-modeline)

#+begin_src emacs-lisp
  ;; LEGACY: smart-mode-line config - SUPERSEDED by doom-modeline (Dec 2025)
  ;; Keeping for reference in case we ever switch back
  ;; (when (package-installed-p 'smart-mode-line)
  ;;   (require 'smart-mode-line)
  ;;   (setq sml/no-confirm-load-theme t)
  ;;   (setq sml/theme 'automatic)
  ;;   (sml/setup))
#+end_src

** Theme Configuration

*Theme: doom-monokai-pro* (from doom-themes package)

Chosen Dec 2025 after testing multiple options:
- =dracula= - good but keywords are pink, wanted more classic feel
- =doom-one= - Atom One Dark style, orange keywords
- =doom-dracula= - pink keywords
- =doom-monokai-pro= - RED keywords, warm brown-ish background (not pure black)

*Why monokai-pro:*
- Red/magenta keywords = classic Java/C++ IDE feel
- Non-black background reduces eye strain
- Excellent syntax differentiation for TypeScript/React

#+begin_src emacs-lisp
  ;; Theme is loaded early in .emacs for immediate visual feedback
  ;; See .emacs for the (load-theme 'doom-monokai-pro t) call
  ;; This ensures theme loads BEFORE config.org processes
#+end_src

** Modeline (doom-modeline)

Clean, informative modeline with clear active/inactive window distinction.

*Why doom-modeline:*
- Clear visual indicator of active buffer (main requirement)
- Shows git branch, file status, LSP diagnostics
- Pairs perfectly with doom-themes
- Requires =nerd-icons= (auto-installed as dependency)

*Dec 2025 learning:* nerd-icons is a HARD dependency of doom-modeline.
Do NOT delete it - breaks the modeline completely.

#+begin_src emacs-lisp
  (use-package doom-modeline
    :ensure t
    :init (doom-modeline-mode 1)
    :custom
    (doom-modeline-height 25)
    (doom-modeline-bar-width 3))
#+end_src

** GUI Elements

Configure GUI-specific elements like the toolbar, line numbers, and frame size.

#+begin_src emacs-lisp
  ;; disable toolbar from GUI (THIS IS EMACS!)
  (tool-bar-mode -1)

  ;; turn line numbers on (left margin) - globally
  ;; Use display-line-numbers-mode (built-in since Emacs 26)
  ;; which is more efficient than the older linum-mode
  (when (version<= "26.0.50" emacs-version)
    (global-display-line-numbers-mode t))
    
  ;; Set default frame size for GUI (approximately 9:16 aspect ratio)
  ;; Only applies when running in GUI mode
  (when (display-graphic-p)
    ;; Add frame size settings to default-frame-alist
    (add-to-list 'default-frame-alist '(width . 100))  ;; characters wide
    (add-to-list 'default-frame-alist '(height . 56))  ;; lines tall (9:16 ratio)
    (add-to-list 'default-frame-alist '(tool-bar-lines . 0))
    (add-to-list 'default-frame-alist '(vertical-scroll-bars . nil))
    
    ;; Apply the same settings to the initial frame
    (add-to-list 'initial-frame-alist '(width . 100))
    (add-to-list 'initial-frame-alist '(height . 56))
    (add-to-list 'initial-frame-alist '(tool-bar-lines . 0))
    (add-to-list 'initial-frame-alist '(vertical-scroll-bars . nil)))
#+end_src

** Highlight and Visual Enhancements

Configure visual enhancements like line highlighting and parenthesis matching.

#+begin_src emacs-lisp
  ;; set highlight color 10% darker default background
  (require 'color)
  (defun set-hl-line-color-based-on-theme ()
    "Sets the hl-line face to have no foregorund and a background
      that is 10% darker than the default face's background."
    (set-face-attribute 'hl-line nil
                        :foreground nil
                        :background (color-darken-name (face-background 'default) 10)))

  (add-hook 'global-hl-line-mode-hook 'set-hl-line-color-based-on-theme)

  (global-hl-line-mode t)
  
  ;; Enable highlighting of matching parentheses
  (show-paren-mode 1)
  (setq show-paren-delay 0)  ; Show matching parenthesis without delay
#+end_src

* Programming Modes

** Modern Development Stack (Tree-sitter + LSP)

/Added: Dec 22, 2025/ - Part of "Real VibeCoding" project.

*** Understanding

**** What This Section Does

| Component   | What It Does | Key Commands |
|-------------+--------------+--------------|
| Tree-sitter | Syntax highlighting, structural navigation | =C-M-a=, =C-M-e=, =C-M-u= |
| LSP (Eglot) | Autocomplete, go-to-def, errors, rename | =M-.=, =M-?=, =C-M-i=, =C-c d= |

Tree-sitter = How code LOOKS (syntax)
LSP = What code MEANS (semantics)

**** How Tree-sitter Works

1. =treesit-language-source-alist= - WHERE to download grammar source (Git repos)
2. =treesit-install-language-grammar= - Compiles grammar ONCE (creates .dylib)
3. Grammars stored in =~/.emacs.d/tree-sitter/=

Manual commands:
- Check grammar: =M-: (treesit-language-available-p 'tsx)=
- Install one: =M-x treesit-install-language-grammar RET tsx=

**** How LSP Works

#+begin_example
┌─────────────┐     stdin/stdout      ┌──────────────────┐
│   Emacs     │ ◄───────────────────► │ Language Server  │
│  (eglot)    │   JSON-RPC messages   │  (subprocess)    │
└─────────────┘                       └──────────────────┘
#+end_example

*Not a network server* - just a subprocess (like git). Spawns on file open, dies on close.

Project detection - walks UP from file, finds marker:
- TS/JS: =tsconfig.json=, =package.json=
- Python: =pyproject.toml= | Go: =go.mod= | C++: =compile_commands.json=

**** Language Servers to Install

| Language | Install Command |
|----------+-----------------------------------------|
| TS/JS    | =npm install -g typescript-language-server typescript= |
| Python   | =npm install -g pyright= |
| Go       | =go install golang.org/x/tools/gopls@latest= |
| C++      | =brew install llvm= (includes clangd) |

*** Configuration

All executable config lives here. Eval with =C-c C-c=.

**** Tree-sitter Setup

#+begin_src emacs-lisp :results silent
  ;; ============================================================
  ;; TREE-SITTER: Syntax highlighting + structural navigation
  ;; ============================================================

  ;; Grammar sources (Git repos with C code → compiled to .dylib)
  (setq treesit-language-source-alist
        '((tsx . ("https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src"))
          (typescript . ("https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src"))
          (javascript . ("https://github.com/tree-sitter/tree-sitter-javascript" "master" "src"))
          (json . ("https://github.com/tree-sitter/tree-sitter-json" "master" "src"))
          (css . ("https://github.com/tree-sitter/tree-sitter-css" "master" "src"))))

  ;; Auto-install missing grammars on startup
  (dolist (grammar '(tsx typescript javascript json css))
    (unless (treesit-language-available-p grammar)
      (treesit-install-language-grammar grammar)))

  ;; File extensions → tree-sitter modes
  (add-to-list 'auto-mode-alist '("\\.tsx\\'" . tsx-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.ts\\'" . typescript-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.js\\'" . js-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.jsx\\'" . js-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.json\\'" . json-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.css\\'" . css-ts-mode))

  ;; Indentation (2 spaces, no tabs)
  (setq-default indent-tabs-mode nil)
  (setq js-indent-level 2)
  (setq typescript-ts-mode-indent-offset 2)
#+end_src

**** LSP (Eglot) Setup

#+begin_src emacs-lisp :results silent
  ;; ============================================================
  ;; LSP (EGLOT): Autocomplete, go-to-def, errors, rename
  ;; ============================================================

  ;; Auto-start eglot for these modes
  (add-hook 'tsx-ts-mode-hook 'eglot-ensure)
  (add-hook 'typescript-ts-mode-hook 'eglot-ensure)
  (add-hook 'js-ts-mode-hook 'eglot-ensure)

  ;; Enable eldoc (shows type info in minibuffer)
  (add-hook 'eglot-managed-mode-hook #'eldoc-mode)

  ;; Keybindings
  (global-set-key (kbd "C-c d") #'eldoc-doc-buffer)    ;; Show docs in buffer
  (global-set-key (kbd "C-c r") #'eglot-rename)        ;; Rename symbol
  (global-set-key (kbd "C-c a") #'eglot-code-actions)  ;; Code actions (quick fixes)

  ;; xref navigation (LSP-powered)
  ;; M-. and M-, already work by default (goto-def, go-back)
  ;; M-? already works (find-references)
  (global-set-key (kbd "M-g s") #'xref-find-apropos)   ;; Symbol search (like git grep but semantic)
#+end_src

**** Completion UI (Corfu + Cape)

#+begin_src emacs-lisp :results silent
  ;; ============================================================
  ;; COMPLETION UI: Corfu popup + Cape backends
  ;; ============================================================
  ;; STATUS: ACTIVE - provides autocomplete popup for LSP completions
  ;; WHAT YOU SEE: Popup appears as you type with LSP suggestions
  ;; TRIGGER: Automatic (corfu-auto t) after 0.2s delay

  (use-package corfu
    :ensure t
    :init
    (global-corfu-mode)                    ;; Enable globally
    :custom
    (corfu-auto t)                         ;; Auto-show popup (no C-M-i needed)
    (corfu-auto-delay 0.2)                 ;; Show after 0.2s pause
    (corfu-auto-prefix 2)                  ;; Show after 2 chars typed
    (corfu-cycle t)                        ;; Cycle through candidates
    (corfu-preselect 'prompt)              ;; Don't auto-select first
    :bind
    (:map corfu-map
          ("TAB" . corfu-next)             ;; TAB cycles forward
          ([tab] . corfu-next)
          ("S-TAB" . corfu-previous)       ;; Shift-TAB cycles back
          ([backtab] . corfu-previous)
          ("RET" . corfu-insert)))         ;; Enter confirms selection

  ;; Cape - extra completion backends (file paths, dabbrev, etc.)
  (use-package cape
    :ensure t
    :init
    ;; Add Cape backends to completion-at-point-functions
    ;; These work alongside LSP completions from Eglot
    (add-to-list 'completion-at-point-functions #'cape-file)     ;; File paths
    (add-to-list 'completion-at-point-functions #'cape-dabbrev)) ;; Words in buffer

  ;; Kind-icon - visual icons showing completion type (function, variable, etc.)
  (use-package kind-icon
    :ensure t
    :after corfu
    :custom
    (kind-icon-default-face 'corfu-default)
    :config
    (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))
#+end_src

*** AI Code Assistance

/Added: Dec 22, 2025/ - AI-powered code predictions and chat.

**** Understanding AI Assistance

| Type | Package | Mode | What It Does | Visual |
|------+---------+------+--------------+--------|
| Prediction | copilot.el | Passive | Guesses what you'll type next | Gray ghost text |
| Integration | claude-code-ide | Bidirectional | Claude Code controls Emacs via MCP | Sidebar + diff |

**Mental Model:**
- Copilot = "What will I type next?" (autocomplete on steroids)
- Claude Code IDE = "Claude Code WITH Emacs powers" (MCP bridge)

**When to use which:**
- Typing boilerplate → Let Copilot predict
- Writing standard patterns → Let Copilot flow
- Need to think/refactor/explain → Claude Code IDE (has your context)

***** How Copilot Works

#+begin_example
┌─────────────┐     stdin/stdout      ┌─────────────────────────┐
│   Emacs     │ ◄───────────────────► │ copilot-language-server │
│ (copilot.el)│   JSON-RPC messages   │   (Node.js subprocess)  │
└─────────────┘                       └─────────────────────────┘
                                              │
                                              ▼
                                      GitHub AI Backend
#+end_example

Same architecture as LSP - subprocess speaking JSON-RPC.

**First-time setup:**
1. =M-x copilot-install-server= - Downloads @github/copilot-language-server
2. =M-x copilot-login= - GitHub OAuth authentication
3. Server lives in =~/.emacs.d/copilot/=

**LSP vs Copilot - Different purposes:**

| LSP (Eglot) | Copilot |
|-------------+---------|
| Valid code (what CAN you write) | Likely code (what WILL you write) |
| Type-aware completions | Pattern-based predictions |
| Popup menu | Ghost text overlay |

**The Three AIs in Your Editor (Dec 2025):**

| Tool | What | When |
|------+------+------|
| LSP (Eglot) | Valid completions | Always (knows types) |
| Copilot | Likely predictions | Typing (pattern matching) |
| Claude Code IDE | Full AI partner | Refactoring, explaining, generating |

LSP ensures correctness, Copilot speeds typing, Claude Code IDE thinks with you.

***** How Claude Code IDE Works

#+begin_example
┌─────────────────────────────────────────────────────────────┐
│                         EMACS                               │
│  ┌─────────────┐     MCP Protocol     ┌─────────────────┐  │
│  │ Your Buffer │ ◄──────────────────► │ Claude Code CLI │  │
│  │ (code)      │  bidirectional       │ (in vterm)      │  │
│  └─────────────┘                      └─────────────────┘  │
│         │                                     │            │
│         ▼                                     ▼            │
│  - LSP (xref)                         - Sees your buffer   │
│  - Tree-sitter                        - Uses LSP features  │
│  - Project.el                         - Runs Emacs cmds    │
│  - imenu                              - Suggests diffs     │
└─────────────────────────────────────────────────────────────┘
#+end_example

*The key insight:* Claude Code gains Emacs superpowers via MCP.

**What Claude Code can now do:**
- =xref-find-references= - Find all usages of symbol
- =xref-find-apropos= - Search symbols by pattern
- =treesit-info= - Analyze syntax tree at cursor
- =imenu-list-symbols= - List all functions/classes
- =project-info= - Understand project structure
- Any Emacs function you expose

**Why this matters:**
- Claude Code alone: Reads files, runs shell commands
- Claude Code + MCP: Uses YOUR editor's intelligence (LSP, tree-sitter)
- Result: AI that understands code like your IDE does

**Capability Comparison:**

| Capability | Claude in Emacs (MCP) | Terminal Claude |
|------------|----------------------|-----------------|
| Find all usages | Uses LSP xref | Would grep (misses some) |
| Go to definition | Uses LSP | Would guess from text |
| See syntax tree | Uses tree-sitter AST | Just sees text |
| List all functions | Uses imenu | Would parse manually |
| Know cursor position | Emacs tells it | Doesn't know |
| Open file in buffer | Yes | Can't control editor |
| Suggest diff | IDE diff viewer | Just shows text |

**First-time setup:**
1. Install vterm (terminal backend)
2. =M-x claude-code-ide= - Start Claude for current project
3. Claude appears in sidebar, ready to work

**** copilot.el Configuration

#+begin_src emacs-lisp :results silent
  ;; ============================================================
  ;; COPILOT.EL: GitHub Copilot ghost text predictions
  ;; ============================================================
  ;; STATUS: ACTIVE - shows AI predictions as gray ghost text
  ;; WHAT YOU SEE: Gray text appears ahead of cursor showing predictions
  ;; TRIGGER: Automatic as you type
  ;; ACCEPT: TAB accepts, M-f accepts word, M-n/M-p cycles, C-g cancels
  ;; COST: FREE tier available (early 2025)
  ;;
  ;; NOTE: Using package-vc to get latest from GitHub (MELPA version is stale)
  ;; First-time setup: M-x copilot-install-server, then M-x copilot-login

  ;; Install from GitHub if not present (Emacs 29+ built-in)
  (unless (package-installed-p 'copilot)
    (package-vc-install "https://github.com/copilot-emacs/copilot.el"))

  (use-package copilot
    :hook ((tsx-ts-mode . copilot-mode)
           (typescript-ts-mode . copilot-mode)
           (js-ts-mode . copilot-mode)
           (python-mode . copilot-mode)
           (emacs-lisp-mode . copilot-mode))
    :bind (:map copilot-completion-map
                ("TAB" . copilot-accept-completion)
                ([tab] . copilot-accept-completion)
                ("M-f" . copilot-accept-completion-by-word)
                ("M-n" . copilot-next-completion)
                ("M-p" . copilot-previous-completion)
                ("C-g" . copilot-clear-overlay))
    :config
    (setq copilot-idle-delay 0.1))
#+end_src

**** claude-code-ide Configuration

#+begin_src emacs-lisp :results silent
  ;; ============================================================
  ;; VTERM: Terminal emulator (required by claude-code-ide)
  ;; ============================================================
  ;; Full terminal emulation via libvterm (C library)
  ;; First-time: needs cmake (brew install cmake), compiles on install

  (use-package vterm
    :ensure t
    :config
    (setq vterm-max-scrollback 10000))
#+end_src

#+begin_src emacs-lisp :results silent
  ;; ============================================================
  ;; CLAUDE-CODE-IDE: Claude Code ↔ Emacs MCP Bridge
  ;; ============================================================
  ;; STATUS: ACTIVE - Claude Code with Emacs superpowers
  ;; WHAT YOU SEE: Claude in sidebar, can see/edit your buffers
  ;; TRIGGER: C-c C-' for menu, or M-x claude-code-ide
  ;; REQUIRES: Claude Code CLI installed (claude command in PATH)
  ;;
  ;; The inception: Claude Code gains access to LSP, tree-sitter,
  ;; project.el - everything your Emacs knows about your code.

  ;; Transient: Popup menu library (claude-code-ide uses it for C-c C-' menu)
  (use-package transient :ensure t)

  ;; Install from GitHub (Emacs 30+ package-vc)
  (unless (package-installed-p 'claude-code-ide)
    (package-vc-install "https://github.com/manzaltu/claude-code-ide.el"))

  (use-package claude-code-ide
    :after transient
    :bind ("C-c C-'" . claude-code-ide-menu)
    :config
    ;; Position Claude on the right side
    (setq claude-code-ide-window-side 'right)
    (setq claude-code-ide-window-width 80)

    ;; Use vterm as terminal backend (best performance)
    (setq claude-code-ide-terminal-backend 'vterm)

    ;; Enable IDE diff viewer for reviewing changes
    (setq claude-code-ide-use-ide-diff t)

    ;; Expose Emacs tools to Claude via MCP
    (claude-code-ide-emacs-tools-setup))
#+end_src

*** Quick Reference

**** All Keybindings Added

| Key | Command | What It Does |
|-----+---------+--------------|
| =C-M-a= | beginning-of-defun | Jump to function start (tree-sitter) |
| =C-M-e= | end-of-defun | Jump to function end (tree-sitter) |
| =C-M-u= | backward-up-list | Jump to parent block (tree-sitter) |
| =M-.= | xref-find-definitions | Go to definition (LSP) |
| =M-?= | xref-find-references | Find all references (LSP) |
| =C-c d= | eldoc-doc-buffer | Show docs in buffer |
| =C-c r= | eglot-rename | Rename symbol across project |
| =C-c a= | eglot-code-actions | Quick fixes, imports |
| =TAB= | corfu-next / copilot-accept | Cycle completions / Accept AI suggestion |
| =M-f= | copilot-accept-by-word | Accept AI word-by-word |
| =M-n/M-p= | copilot-next/prev | Cycle AI suggestions |
| =C-c C-'= | claude-code-ide-menu | Open Claude Code IDE menu |

**** First-Time Setup Checklist

- [ ] =npm install -g typescript-language-server typescript= (for TS/JS LSP)
- [ ] =M-x copilot-install-server= (one-time)
- [ ] =M-x copilot-login= (GitHub OAuth)
- [ ] =nvm alias default 22= (copilot needs Node 22+)
- [ ] Verify =claude= command in PATH (for claude-code-ide)
- [ ] =brew install cmake= (for vterm compilation)

**** Troubleshooting

| Problem | Solution |
|---------+----------|
| npm not found | Restart Emacs (exec-path-from-shell needs shell restart) |
| Copilot server dies | Check Node version: needs 22+ (=node --version=) |
| No completions | Verify eglot connected: =M-x eglot-events-buffer= |
| Tree-sitter mode not loading | Check grammar: =M-: (treesit-language-available-p 'tsx)= |
| vterm won't compile | =brew install cmake=, restart Emacs, say yes to compile |
| claude-code-ide not starting | Verify: =which claude= returns path |

** Global Development Settings

Set up common development tools and settings that apply to all programming modes.

#+begin_src emacs-lisp
  ;;----------------------------------------------------------
  ;; GLOBAL DEV MODE SETUP {
  ;;----------------------------------------------------------

  ;; LEGACY: flycheck removed Dec 2025 - using flymake (built-in) instead
  ;; (require 'flycheck)
  ;; (add-hook 'after-init-hook #'global-flycheck-mode)
  ;; (setq-default flycheck-temp-prefix ".flycheck")

  ;;----------------------------------------------------------
  ;; } END GLOBAL DEV MODE ----------------------------------
  ;;----------------------------------------------------------
#+end_src

** C/C++ Mode

Configure settings for C and C++ development.

#+begin_src emacs-lisp
  ;;------------------------------------------------------------------------------
  ;; CC Mode
  ;;------------------------------------------------------------------------------
  (progn
    (load "cc-mode")
    (c-add-style "sid-cxx-style"
                 '((c-basic-offset . 4)
                   (c-tab-always-indent . nil)
                   (tab-width . 8)
                   (c-comment-only-line-offset . 0)
                   (c-offsets-alist . ((statement-block-intro . +)
                                       (knr-argdecl-intro . +)
                                       (substatement-open . 0)
                                       (label . 0)
                                       (statement-cont . +)
                                       (innamespce . 0)
                                       ))
                   ))


    ;; See also kc-c-mode-common-hook above.
    (defun kc-c-mode-hook ()
      (c-set-style "sid-cxx-style"))
    (add-hook 'c-mode-hook 'kc-c-mode-hook)

    ;; See also kc-c-mode-common-hook above.
    (defun kc-c++-mode-hook ()
      (c-set-style "sid-cxx-style")
      ;; Make ':' a symbol constituent char so that find-tag gets the right
      ;; default value.
      (modify-syntax-entry ?: "_"))
    (add-hook 'c++-mode-hook 'kc-c++-mode-hook))

  ;; experimenting with cc coding style
  ;; writing below the original code so that I only override and not omit
  (define-key c-mode-base-map (kbd "RET") 'newline-and-indent)
#+end_src

** Go Mode Configuration

Setup for the Go programming language.

#+begin_src emacs-lisp
  ;;----------------------------------------------------------
  ;; GOLANG DEVELOPMENT {
  ;;----------------------------------------------------------

  ;; from: https://github.com/dominikh/go-mode.el/blob/master/go-guru.el
  (require 'go-guru)

  ;; To enable identifier highlighting mode in a Go source buffer, use:
  (add-hook 'go-mode-hook #'go-guru-hl-identifier-mode)

  ;; from
  ;; http://tleyden.github.io/blog/2014/05/22/configure-emacs-as-a-go-editor-from-scratch/
  (defun my-go-mode-hook ()
    ; Use goimports instead of go-fmt
    (setq gofmt-command "goimports")
    ; Call Gofmt before saving
    (add-hook 'before-save-hook 'gofmt-before-save)
    ; Customize compile command to run go build
    (if (not (string-match "go" compile-command))
        (set (make-local-variable 'compile-command)
             "go build -v && go test -v && go vet"))
    ; Godef jump key binding
    (local-set-key (kbd "M-.") 'godef-jump)
    (local-set-key (kbd "M-*") 'pop-tag-mark)
  )
  (add-hook 'go-mode-hook 'my-go-mode-hook)

  ;; autocomplete for go
  (defun auto-complete-for-go ()
    (auto-complete-mode 1))
  (add-hook 'go-mode-hook 'auto-complete-for-go)

  (with-eval-after-load 'go-mode
    (require 'go-autocomplete))

  ;;----------------------------------------------------------
  ;; } END GOLANG DEV ----------------------------------------
  ;;----------------------------------------------------------
** Web Development


Configure settings for web development, including JavaScript, HTML, CSS, and JSX.

#+begin_src emacs-lisp
  ;;----------------------------------------------------------
  ;; WEB DEVELOPMENT
  ;;----------------------------------------------------------
  (require 'web-mode)
  (setq js-indent-level 2)

  (add-to-list 'auto-mode-alist '("\\.html\\'" . web-mode))

  ;; see http://web-mode.org/
  (defun my-web-mode-hook ()
    "Hooks for Web mode."

    ;; indents 
    (setq web-mode-markup-indent-offset 2)
    (setq web-mode-css-indent-offset 2)
    (setq web-mode-code-indent-offset 2)
    (setq web-mode-attr-indent-offset 2)

    ;; smartparens
    (setq web-mode-enable-auto-pairing nil)

    ;; key bindings
    (local-set-key (kbd "C-c C-v") 'browse-url-of-buffer)

    (add-hook 'local-write-file-hooks
              (lambda ()
                (delete-trailing-whitespace)
                nil))
  )
  (add-hook 'web-mode-hook  'my-web-mode-hook)

  ;; for better jsx syntax-highlighting in web-mode
  ;; - courtesy of Patrick @halbtuerke
  (defadvice web-mode-highlight-part (around tweak-jsx activate)
    (if (equal web-mode-content-type "jsx")
      (let ((web-mode-enable-part-face nil))
        ad-do-it)
      ad-do-it))

  ;; LEGACY: flycheck eslint config removed Dec 2025
  ;; (setq-default flycheck-disabled-checkers
  ;;   (append flycheck-disabled-checkers '(javascript-jshint)))
  ;; (flycheck-add-mode 'javascript-eslint 'web-mode)


  ;;---------------------------------------------------------
  ;; } End Web Development Settings--------------------------
  ;;---------------------------------------------------------
#+end_src

** File Type Associations

Configure various file types to use the appropriate major modes.

#+begin_src emacs-lisp
  ;;cmake-mode
  (require 'cmake-mode)
  (setq auto-mode-alist
        (append '(("CMakeLists\\.txt\\'" . cmake-mode)
                  ("\\.cmake\\'" . cmake-mode))
                auto-mode-alist))
  ;;dos mode
  (require 'dos)
  (add-to-list 'auto-mode-alist '("\\.bat\\'" . dos-mode))

  ;;yaml mode
  (require 'yaml-mode)
  (add-to-list 'auto-mode-alist '("\\.yml$" . yaml-mode))

  ;; markdown mode
  (autoload 'markdown-mode "markdown-mode"
     "Major mode for editing Markdown files" t)
  (add-to-list 'auto-mode-alist '("\\.text\\'" . markdown-mode))
  (add-to-list 'auto-mode-alist '("\\.markdown\\'" . markdown-mode))
  (add-to-list 'auto-mode-alist '("\\.md\\'" . markdown-mode))
#+end_src

* Org Mode

Configure Org mode, one of Emacs' most powerful packages for notes, TODOs, and more.

#+begin_src emacs-lisp
  ;;----------------------------------------------------------
  ;; ORG MODE  {
  ;;----------------------------------------------------------

  ;; automaticaly sets web page title on link paste 
  (global-set-key (kbd "C-x p i") 'org-cliplink)  ;; TODO - change keys

  ;; ---- conveniences ---

  ;; global key activation, from https://orgmode.org/manual/Activation.html
  (bind-key (kbd "C-c l") 'org-store-link)
  (bind-key (kbd "C-c a") 'org-agenda)
  (bind-key (kbd "C-c c") 'org-capture)

  ;; TODO - not a fan of this anymore, remove.. 
  (with-eval-after-load 'org
    (bind-key "C-c k" 'org-cut-subtree org-mode-map)
    (setq org-yank-adjusted-subtrees t))

  ;; keybind for org-agenda with n option selected
  ;; (defun org-agenda-show-agenda-and-todo (&optional arg)
  ;;   (interactive "P")
  ;;   (org-agenda arg "n"))
  ;; (define-key org-mode-map (kbd "C-c C-a " 'org-agenda-show-agenda-and-todo))

  ;; Task Management {

  (setq org-todo-keywords
        '((sequence "TODO(t!)"
                    "STARTED(s!)"
                    "WAITING(w@/!)" "|" "DONE(x@/!)")))

  (setq org-log-done 'time)

  ;; Task  Management }

  ;; Agenda Setup - using a variable for org directory to improve portability
  (defvar org-directory (expand-file-name "~/.org"))
  (setq org-agenda-files (list (expand-file-name "life.org" org-directory)
                               (expand-file-name "work.org" org-directory)))

  ;; start week on weekend i.e saturday 
  (setq org-agenda-start-on-weekday 6)

  ;;----------------------------------------------------------
  ;; ORG MODE  }
  ;;----------------------------------------------------------
#+end_src

* Editor Enhancements

** Autopair

Configure autopair for automatic insertion of matching pairs of brackets, quotes, etc.

#+begin_src emacs-lisp
  ;; auto pair braces, quotes, brackets
  (require 'autopair)
  ;; disabling it, as this is more for pain then beign usefulc
  ;; (autopair-global-mode 1)
  (setq autopair-autowrap t)
#+end_src

** Whitespace Mode

Configure display of whitespace to maintain consistent code style.

#+begin_src emacs-lisp
  ;; whitespace-mode
  ;; highlight lines more than 80 chars
  ;; free of trailing whitespace and to use 80-column width, standard indentation
  (setq whitespace-style '(trailing lines space-before-tab
                                    indentation space-after-tab)
        whitespace-line-column 80)
  (global-whitespace-mode 1)
#+end_src

** Window Management

Functions for better window management.

#+begin_src emacs-lisp :results silent
  ;; someday might want to rotate windows if more than 2 of them
  (defun swap-windows () "If you have 2 windows, it swaps them."
    (interactive) (cond ((not (= (count-windows) 2))
                         (message "You need exactly 2 windows to do this."))
                        (t
                         (let* ((w1 (first (window-list)))
                                (W2 (second (window-list)))
                                (b1 (window-buffer w1))
                                (b2 (window-buffer w2))
                                (s1 (window-start w1))
                                (s2 (window-start w2)))
                           (set-window-buffer w1 b2)
                           (set-window-buffer w2 b1)
                           (set-window-start w1 s2)
                           (set-window-start w2 s1)))))

  ;;(global-set-key (kbd "M-2") 'swap-windows)

  
  ;; xref results in side window at bottom (25% of frame height)
  (add-to-list 'display-buffer-alist
               '("\\*xref\\*"
                 (display-buffer-in-side-window)
                 (side . bottom)
                 (slot . 0)
                 (window-height . 0.25)))
#+end_src

* Utility Functions

Add useful utility functions and hooks.

#+begin_src emacs-lisp
  ;;------------------------------------------------------------------------------
  ;; Hooks
  ;;------------------------------------------------------------------------------

  (add-hook 'diff-mode-hook '(lambda ()
                               (require 'ansi-color)
                               (ansi-color-apply-on-region
                                (point-min) (point-max))))

  ;;get rid of DOS EOL i.e annoying ^M
  (defun remove-dos-eol ()
    "Do not show ^M in files containing mixed UNIX and DOS line endings."
    (interactive)
    (setq buffer-display-table (make-display-table))
    (aset buffer-display-table ?\^M []))

  (add-hook 'diff-mode-hook 'remove-dos-eol)

  (add-hook 'yaml-mode-hook
                    (lambda ()
                           (define-key yaml-mode-map "\C-m" 'newline-and-indent)))
  ;; enable upcase-region command (this uppercases the selection)
  (put 'upcase-region 'disabled nil)
  ;; enable narrow-to-region command (I forgot why/how I would use is
  (put 'narrow-to-region 'disabled nil)

  ;; Treat CamelCase words the same special way it treats lisp-case and
  ;; snake_case words.
  (add-hook 'prog-mode-hook 'subword-mode)
#+end_src

* Custom Variables

*NOTE:* custom-set-variables lives in =.emacs= (not here).
Emacs auto-appends customizations there. This avoids duplicate blocks.

Dec 2025: Removed duplicate custom-set-variables from config.org to fix conflicts.
